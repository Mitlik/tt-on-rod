#TICK {idle} {#cr} {300}
#READ $script_dir/Modules/rolling.dat
#CLASS {roll} {open}

#NOP Setup
#NOP =====
#variable {str} {}
#variable {dex} {}
#variable {wis} {}
#variable {int} {}
#variable {cha} {}
#variable {con} {}
#variable {lck} {}

#variable {minstat} {9}
#variable {minstr} {$minstat}
#variable {mindex} {$minstat}
#variable {minint} {$minstat}
#variable {minwis} {$minstat}
#variable {mincha} {$minstat}
#variable {mincon} {$minstat}
#variable {minlck} {$minstat}


#NOP Character Creation
#NOP ==================
#ALIAS {rollchar} {
    #IF {@setclass{%4} || "%4" == "" || "%3" == "" || "%2" == "" || "%1" == ""} {
        #SHOWME {Syntax: rollchar <sex> <race> <class> <rolltype>};
    };
    #ELSE {
        #VARIABLE {sex} {%1};
        #VARIABLE {race} {%2};
        #VARIABLE {class} {%3};
        #FOREACH {$classtypes[%4][]} {stat} {
            #VARIABLE {min${stat}} {$classtypes[%4][$stat]};
        };
    };
};

#ACTION {^Did I get that right, %w (Y/N)?}{
    y;
    $passwd;
    $passwd;
    a;
    $class;
    $race;
    $sex;
    n;
};
#ACTION {^Your current appearance is:$} {#DELAY {0.5} {#CR};#DELAY {1} {#CR}}

#ACTION {~^\e[0;1;32mWater diverts around a large boulder.$} {
    #SPLIT;
    #SEND {help start};
    experienced;
    up;
    say lighted path of the peaceful;
    nw;
    statrep;
}

#ACTION {^Your base stats:    %d{ ?} str %d{ ?} int %d{ ?} wis %d{ ?} dex %d{ ?} con %d{ ?} cha %d{ ?} lck.$} {
    #VARIABLE {str} {%1};
    #VARIABLE {int} {%3};
    #VARIABLE {wis} {%5};
    #VARIABLE {dex} {%7};
    #VARIABLE {con} {%9};
    #VARIABLE {cha} {%11};
    #VARIABLE {lck} {%13};
    #IF {@chkstats{}} {
	getAuthed;
    };
    #ELSE {reroll};
}

#FUCNTION {setclass} {
    #NOP {returns false to go forward rolling, true to force re-entry};
    #IF {"%1" == ""} {
        #SHOWME {Syntax: setclass <rolltype>};
        #VARIABLE {result} {0};
    };
    #ELSE {
        #LIST {classtypes} {fnd} {{%1}*} {clsind};
        #IF {$clsind < 1} {
            #SHOWME {Syntax: setclass <rolltype>};
            displayclasses;
            #VARIABLE {result} {1};
        };
        #ELSE {
            roll%1;
            #VARIABLE {result} {0};
        };
    };
} 
#ALIAS {displayclasses} {
    #FOREACH {$classtypes[]} {class} {
        #ECHO {<170>%s} {$class};
    };
}
#ALIAS {fulldisplayclasses} {
    
    #FOREACH {$classtypes[]} {class} {
        #UNVARIABLE {tmp_stats};
        #FORALL {$classtypes[$class][]} {
            #VARIABLE {tmp_stats} {${tmp_stats}\t&0: $classtypes[$class][&0]};
		};
        #ECHO {<170>%-10s %s} {${class}} {$tmp_stats};
	};
}

#NOP Rolling
#NOP =======
#GAG {^Your character's base stats have been rerolled...$}
#GAG {^                     New             Previous$}

#ACTION      {~^Strength        \e[0;37m      {| }%d             \e[32m      {| }%d$} {#LINE GAG;#VARIABLE {str} {%2}};
#ACTION {~^\e[1mDexterity       \e[0;37m      {| }%d             \e[32m      {| }%d$} {#LINE GAG;#VARIABLE {dex} {%2}};
#ACTION {~^\e[1mIntelligence    \e[0;37m      {| }%d             \e[32m      {| }%d$} {#LINE GAG;#VARIABLE {int} {%2}};
#ACTION {~^\e[1mWisdom          \e[0;37m      {| }%d             \e[32m      {| }%d$} {#LINE GAG;#VARIABLE {wis} {%2}};
#ACTION {~^\e[1mCharisma        \e[0;37m      {| }%d             \e[32m      {| }%d$} {#LINE GAG;#VARIABLE {cha} {%2}};
#ACTION {~^\e[1mConstitution    \e[0;37m      {| }%d             \e[32m      {| }%d$} {#LINE GAG;#VARIABLE {con} {%2}};
#ACTION {~^\e[1mLuck            \e[0;37m      {| }%d             \e[32m      {| }%d$} {
    #VARIABLE {lck} {%2};
    #IF {@chkstats{}} {
        getAuthed;
    };
    #ELSE {
        displaystats;
        reroll;
    };
    #LINE GAG;
}

#ALIAS {getAuthed} {
    #NOP ;
}

#FUNCTION {chkstats} {
    #IF { $str >= $minstr && 
          $wis >= $minwis && 
          $dex >= $mindex && 
          $int >= $minint && 
          $cha >= $mincha && 
          $con >= $mincon && 
          $lck >= $minlck } {
        #BELL;
        #DELAY {.5} {#BELL};
        #DELAY {1} {#BELL};
        #VARIABLE {result} {$TRUE};
    };
    #ELSE {
        #VARIABLE {result} {$FALSE};
    };
}

#ALIAS {displaystats} {#SHOWME {\nBase stats:\t $str str\t $int int\t $wis wis\t $dex dex\t $con con\t $cha cha\t $lck lck};}

#ALIAS {cancel} {#UNACTION {Luck*};}
#ALIAS {reset} {
    #ACTION {~^\e[1mLuck            \e[0;37m      {| }%d             \e[32m      {| }%d$} {
        #VARIABLE {lck} {%2};
        #IF {@chkstats{}} {
            getAuthed;
        };
        #ELSE {
            displaystats;
            reroll;
        };
        #LINE GAG;
    };
}

#CLASS {roll} {close}

